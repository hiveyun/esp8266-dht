dht {
   * power-on --> idle,

    idle [
        buttonLongPress -(smartconfig.smartconfig)-,
        check -(net.check, button.check)-,
        _ --,
    ],
}

net {
    * ready -(@initWiFi)-> offline,
    offline (@led500) [
        check -(@netCheck, smartconfig.check)-,
        online --> online,
        _ --,
    ],
    online (@led1000) [
        check -(@netCheck)-,
        offline --> offline,
        online -(mqtt.check)-,
        _ --,
    ],
}

mqtt {
    * ready -(@initMqtt)-> idle,
    idle [
        check -(@checkPassword)-,
        valid --> connecting,
        invalid --> fetchToken,
        _ --,
    ],
    fetchToken (@startUdpServer) [
        check -(@fetchToken)-,
        done --> connecting,
        _ --,
    ] (@stopUdpServer),
    connecting [
        check -(@connectCheck)-,
        connected --> connected,
        unconnected -(@tryConnect)-,
        failed --> fetchToken,
        _ --,
    ],
    connected (@onConnected, @led1000) [
        check -(@connectCheck)-,
        unconnected --> connecting,
        connected -(sensor.check)-,
        _ --,
    ] (@led500),
}

button {
    * ready --> released,
    released [
        check -(@buttonCheck)-,
        pressed --> pressed,
        _ --,
    ],
    pressed (@buttonEnterPressed) [
        check -(@buttonCheck)-,
        released --> released,
        longpressed -(dht.buttonLongPress)-> longpressed,
        pressed -(@buttonPressed)-,
        _ --,
    ],
    longpressed [
        check -(@buttonCheck)-,
        released --> released,
        _ --,
    ],
}

led {
    * ready --> off,

    off (@ledOff) [
        toggle --> on,
        _ --,
    ],

    on (@ledOn) [
        toggle --> off,
        _ --,
    ]
}

smartconfig {
    * ready --> idle,
    idle [
        smartconfig --> smartconfig,
        _ --,
    ],
    smartconfig (@led200, @beginSmartconfig) [
        check -(@smartconfigDone)-,
        done --> idle,
        timeout --> idle,
        _ --,
    ] (@led1000)
}

sensor {
    * ready --> idle,
    idle (@enterSensorIdle) [
        check -(@sensorIdleCheck)-,
        report --> report,
        _ --,
    ],
    report (@enterSensorReport) [
        check -(@sensorReportCheck)-,
        report -(@sensorReport)-> idle,
        _ --,
    ] (@exitSensorReport),
}
